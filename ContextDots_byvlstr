'Make the dots on top of the powerpoint presentation. To use only after presentation is finished'
Sub ContextDots()
    On Error Resume Next
            With ActivePresentation
             
                'Get all the section except the 1st one'
                SectionCount = .SectionProperties.Count - 1
                 
                Set pres = ActivePresentation
                'Get the scheme colors of the design used in the slide'
                Set schemeColors = pres.Designs(1).SlideMaster.Theme.ThemeColorScheme
                 
 
                'Expects you to have an intro slide and a thank you slide.'
                'If not, remove the "-1"'                   
                For X = 2 To .Slides.Count - 1
                     
                    'Makes the background and put the 6th color in the design selected'
                    Set bg = .Slides(X).Shapes.AddShape(msoShapeRectangle, 0, 0, .PageSetup.SlideWidth, 25)
                    bg.Name = "Background"
                    ' Change the rectangle's colours here
                    bg.Fill.ForeColor.RGB = schemeColors(6).RGB
                    bg.Line.Visible = False
                     
                    ' Change the bullets' size, shape and spacing here
                    BulletSize = 6
                    BulletShape = msoShapeOval
                    BulletSpacing = 2
                     
                    Offset = 20
                    SlideNumber = 2
                    For Y = 2 To SectionCount
 
                        'Offset for the different sections'                   
                        Offset = (Y - 1) * .PageSetup.SlideWidth / (SectionCount)
                        TextboxOffset = Offset - 7.5
                         
                        'Count the number of slide per section'
                        SectionSlidesCount = .SectionProperties.SlidesCount(Y)
                        sectionTitle = .SectionProperties.Name(Y)
                        Set textbox = .Slides(X).Shapes.AddTextbox(msoTextOrientationHorizontal, _
                                TextboxOffset, -2, 65, 50)
                        textbox.TextFrame.TextRange.Text = sectionTitle
                        textbox.Name = "SectionTitleBox"
 
                        ' Change the font colour, size and type here
                        textbox.TextFrame.TextRange.font.Color = vbWhite
                        textbox.TextFrame.TextRange.font.size = 11
                        textbox.TextFrame.TextRange.font.Name = "Calibri Light"
                         
                        'Loop through all the slide in the sections'
                        For Z = 1 To SectionSlidesCount
                            Set Bullet = .Slides(X).Shapes.AddShape(BulletShape, _
                                Offset + (Z - 1) * (BulletSpacing + BulletSize), 16, BulletSize, BulletSize)
                            Bullet.Name = "Bullet"
                            Bullet.Line.Weight = 1
                            'Change the bullets' fill and line colour here (case: Other slide)
                            Bullet.Fill.ForeColor.RGB = vbWhite
                            Bullet.Line.ForeColor.RGB = vbWhite
                             
                            'If current slide'
                            If X = SlideNumber Then
                                textbox.TextFrame.TextRange.font.Bold = True
                                ' Change the bullets' fill and line colour here (case: active slide)
                                Bullet.Fill.ForeColor.RGB = vbBlack
                            End If
                             
                            SlideNumber = SlideNumber + 1
                        Next Z:
                    Next Y:
                     
                Next X:
            End With
End Sub
